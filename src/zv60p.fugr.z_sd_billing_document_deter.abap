FUNCTION Z_SD_BILLING_DOCUMENT_DETER.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     VALUE(I_BELNR) LIKE  VBRK-BELNR DEFAULT SPACE
*"     VALUE(I_GJAHR) LIKE  VBRK-GJAHR DEFAULT SPACE
*"     VALUE(I_BUKRS) LIKE  VBRK-BUKRS DEFAULT SPACE
*"     VALUE(I_XBLNR) LIKE  VBRK-XBLNR DEFAULT SPACE
*"  EXPORTING
*"     VALUE(O_VBELN) LIKE  VBRK-VBELN
*"     VALUE(O_RETURNCODE) LIKE  SY-SUBRC
*"--------------------------------------------------------------------
DATA: DA_SHLP TYPE SHLP_DESCR_T,
      DA_SELOPT LIKE DDSHSELOPT OCCURS 0 WITH HEADER LINE,
      DA_INTERFACE   LIKE DDSHIFACE,
      DA_FIELDPROPWA LIKE DDSHFPROP,
      DA_VALUES LIKE DDSHRETVAL OCCURS 0 WITH HEADER LINE.

  CALL FUNCTION 'F4IF_GET_SHLP_DESCR'
       EXPORTING
            SHLPNAME = 'VF_BKPF'
       IMPORTING
            SHLP     = DA_SHLP.
  IF NOT I_BELNR IS INITIAL.
    DA_SELOPT-SHLPNAME  = DA_SHLP-SHLPNAME.
    DA_SELOPT-SIGN      = 'I'.
    DA_SELOPT-OPTION    = 'CP'.
    DA_SELOPT-SHLPFIELD = 'BELNR'.
    DA_SELOPT-LOW       = I_BELNR.
    APPEND DA_SELOPT.
  ENDIF.
  IF NOT I_GJAHR IS INITIAL.
    DA_SELOPT-SHLPNAME  = DA_SHLP-SHLPNAME.
    DA_SELOPT-SIGN      = 'I'.
    DA_SELOPT-OPTION    = 'EQ'.
    DA_SELOPT-SHLPFIELD = 'GJAHR'.
    DA_SELOPT-LOW       = I_GJAHR.
    APPEND DA_SELOPT.
  ENDIF.
  IF NOT I_BUKRS IS INITIAL.
    DA_SELOPT-SHLPNAME  = DA_SHLP-SHLPNAME.
    DA_SELOPT-SIGN      = 'I'.
    IF ( I_BUKRS CP '*#**' ).
      DA_SELOPT-OPTION    = 'CP'.
    ELSE.
      DA_SELOPT-OPTION    = 'EQ'.
    ENDIF.
    DA_SELOPT-SHLPFIELD = 'BUKRS'.
    DA_SELOPT-LOW       = I_BUKRS.
    APPEND DA_SELOPT.
  ENDIF.
  IF NOT I_XBLNR IS INITIAL.
    DA_SELOPT-SHLPNAME  = DA_SHLP-SHLPNAME.
    DA_SELOPT-SIGN      = 'I'.
    DA_SELOPT-OPTION    = 'CP'.
    DA_SELOPT-SHLPFIELD = 'XBLNR'.
    DA_SELOPT-LOW       = I_XBLNR.
    APPEND DA_SELOPT.
    DA_SELOPT-SHLPNAME  = DA_SHLP-SHLPNAME.
    DA_SELOPT-SIGN      = 'I'.
    DA_SELOPT-OPTION    = 'EQ'.
    DA_SELOPT-SHLPFIELD = 'BSTAT'.
    DA_SELOPT-LOW       = ' '.
    APPEND DA_SELOPT.
  ENDIF.
  DA_SHLP-SELOPT = DA_SELOPT[].
  LOOP AT DA_SHLP-FIELDPROP INTO DA_FIELDPROPWA.
    CLEAR DA_FIELDPROPWA-SHLPSELPOS.
    MODIFY DA_SHLP-FIELDPROP FROM DA_FIELDPROPWA.
  ENDLOOP.
  READ TABLE DA_SHLP-INTERFACE WITH KEY 'AWKEY' INTO DA_INTERFACE.
  IF SY-SUBRC = 0.
    DA_INTERFACE-VALFIELD   = 'AWKEY'.
    MODIFY DA_SHLP-INTERFACE FROM DA_INTERFACE INDEX SY-TABIX.
  ENDIF.
  CALL FUNCTION 'F4IF_START_VALUE_REQUEST'
       EXPORTING
            SHLP          = DA_SHLP
       IMPORTING
            RC            = O_RETURNCODE
       TABLES
            RETURN_VALUES = DA_VALUES.
  READ TABLE DA_VALUES INDEX 1.
  IF SY-SUBRC = 0.
    O_VBELN = DA_VALUES-FIELDVAL.
  ENDIF.
  IF O_VBELN CO ' 0123456789'.
    CATCH SYSTEM-EXCEPTIONS CONVERSION_ERRORS = 1.
      UNPACK O_VBELN TO O_VBELN.
    ENDCATCH.
    IF O_VBELN CO '0'.
      CLEAR O_VBELN.
    ENDIF.
  ENDIF.
ENDFUNCTION.
